plugins {
    id 'java-library'
    id 'groovy'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.jfrog.artifactory' version '4.24.21'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

project.group = 'org.clyze'

wrapper {
    gradleVersion = '6.7'
}

repositories {
    mavenCentral()
}

def groovyVersion='3.0.8'
dependencies {
    implementation "org.codehaus.groovy:groovy:${groovyVersion}"
    implementation "org.codehaus.groovy:groovy-xml:${groovyVersion}"
    api 'commons-io:commons-io:2.11.0'          // Apache 2.0
    api 'log4j:log4j:1.2.17'                    // Logging implementation, Apache 2.0
    api 'org.apache.ivy:ivy:2.5.0'              // Apache Ivy (for downloading inputs from Maven repos), Apache 2.0
    api 'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'  // MPL 1.1
    api 'org.zeroturnaround:zt-zip:1.14'        // Apache 2.0

    testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

java {
    withSourcesJar()
    withJavadocJar()
}

javadoc {
    if (!System.getProperty('java.version').startsWith('1.'))
        options.addBooleanOption('html5', true)
}

test {
    useJUnitPlatform()
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

release {
    //use defaults, see https://github.com/researchgate/gradle-release
}

tasks.release.doLast {
    println "WARNING: Releasing clue-common affects both public doop and doop-nexgen, please check that everything works."
}

afterReleaseBuild.dependsOn publish

if (project.hasProperty('artifactory_user')) {
    publishing {
        repositories {
            maven {
                credentials {
                    username artifactory_user
                    password artifactory_password
                }
                //Always publish to the public releases repo
                url "${artifactory_contextUrl}/libs-public-release-local"
            }
        }
    }
}

// To configure, set jfrog_user/jfrog_password in [~/.gradle,.]/gradle.properties.
// Upload with: ./gradlew artifactoryPublish
artifactoryPublish {
    skip = false //Skip build info analysis and publishing (false by default)
    contextUrl = 'http://clyze.jfrog.io/artifactory'
    publications ('mavenJava')
    properties = ['qa.level': 'basic']
    clientConfig.publisher.repoKey = 'default-maven-local'
    clientConfig.publisher.username = jfrog_user
    clientConfig.publisher.password = jfrog_password

    // Minimum file size in KB for which the plugin performs checksum deploy optimization. Default: 10. Set to 0 to disable uploading files with checksum that already exists in Artifactory.
    clientConfig.publisher.minChecksumDeploySizeKb = 10
}
