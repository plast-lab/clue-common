plugins {
    id 'groovy'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.6.0'
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

project.group = 'org.clyze'

wrapper {
    gradleVersion = '5.4.1'
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'com.google.code.gson:gson:2.8.0',
            'commons-io:commons-io:2.4',
            'log4j:log4j:1.2.14',                               // Logging implementation
            'org.apache.ivy:ivy:2.4.0',                         // Apache Ivy (for downloading inputs from Maven repos)
            'org.codehaus.groovy:groovy-all:2.5.7',
            'com.googlecode.juniversalchardet:juniversalchardet:1.0.3'

    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'
}

// compileJava {
//     options.compilerArgs << '-Xlint:unchecked'
// }

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

release {
    //use defaults, see https://github.com/researchgate/gradle-release
}

tasks.release.doLast {
    println "WARNING: Releasing clue-common affects both public doop and doop-nexgen, please check that everything works."
}

afterReleaseBuild.dependsOn publish

if (project.hasProperty('artifactory_user')) {
    publishing {
        repositories {
            maven {
                credentials {
                    username artifactory_user
                    password artifactory_password
                }
                //Always publish to the public releases repo
                url "${artifactory_contextUrl}/libs-public-release-local"
            }
        }
    }
}
